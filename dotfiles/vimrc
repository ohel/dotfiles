au GUIEnter * set lines=31 columns=107 "set window size when starting in GUI mode
let g:dlines=31 "put default window size in memory
let g:dcols=107
let g:zoom_functions = 1
set tabstop=4 "tab width

if hostname() == "minigun"
    let g:pydiction_location = '~/.vim/pydiction/complete-dict'
    let g:pydiction_menu_height = 15

    set guifont=ProggyCleanTTSZ\ 12
    let g:dfont="ProggyCleanTTSZ"
    let g:daltfont="Monospace"

elseif hostname() == "predator"
    au GUIEnter * set lines=27 columns=107
    let g:dlines=27
    let g:dcols=107

    set guifont=ProggyCleanTTSZ\ 12
    let g:dfont="ProggyCleanTTSZ"
    let g:daltfont="Monospace"

elseif hostname() == "oheldell"
    au GUIEnter * set lines=35 columns=130
    let g:dlines=35
    let g:dcols=130
    let g:zoom_level=2

    set guifont=Monospace\ 12
    let g:dfont="ProggyCleanTTSZ"
    let g:daltfont="Monospace"
    
    set tabstop=2

else
    let g:zoom_functions = 0

endif

if has("win32")
    set guifont=ProggyCleanTTSZ:h12:cANSI
    let g:dfont="ProggyCleanTTSZ"
    let g:daltfont="Bitstream_Vera_Sans_Mono"
endif



set showtabline=2 " tab selector always visible
set expandtab " expand tab to spaces, use c-q<tab> for real tab
let &shiftwidth=&tabstop " set intend (using keys << or >>) width
let &softtabstop=&softtabstop " set indent width in insert mode using tab
function! TabOrComplete() " generic tab complete
    if col('.')>1 && strpart( getline('.'), col('.')-2, 3 ) =~ '^\w'
        return "\<c-n>"
    else
        return "\<Tab>"
    endif
endfunction
inoremap <tab> <c-r>=TabOrComplete()<cr>
" insert real tab on shift-tab
inoremap <s-tab> <c-q><tab>
" tab omnicomplete with control-tab (needs omnifunc)
inoremap <c-tab> <c-x><c-o>
" tab navigation
nnoremap <c-tab> :tabnext<cr>
nnoremap <c-s-tab> :tabprevious<cr>



colorscheme custom
set encoding=utf-8
" set swap directory
"if isdirectory("~/.vim")
"   set directory=~/.vim
"endif
set noswapfile
set undofile undolevels=1000 undoreload=10000 undodir=~/.vim/undodir nobackup
silent !mkdir -p "$HOME/.vim/undodir"



call plug#begin('~/.vim/plugged')
Plug 'leafgarland/typescript-vim'
Plug 'Shougo/unite.vim'
Plug 'Shougo/unite-outline'
Plug 'Shougo/vimproc.vim', { 'do': 'cd ~/.vim/plugged/vimproc.vim && make -f make_unix.mak' } " allows async operations
call plug#end()

if filereadable(expand("~/.vim/plugin/taglist.vim"))
    nnoremap <c-f5> :!ctags -R -f ~/.vim/ctagsdb --c++-kinds=+p --fields=+iaS --extra=+q .<cr>
    nnoremap <f4> :TlistToggle<cr>
    inoremap <f4> <c-o>:TlistToggle<cr>
    autocmd FileType cpp set tags +=~/.vim/ctagsdb
endif

if executable('ag')
    set grepprg=ag\ --vimgrep\ $*
    set grepformat=%f:%l:%c:%m
    let g:unite_source_grep_command = 'ag'
    let g:unite_source_grep_default_opts = '--vimgrep -i --silent'
    let g:unite_source_grep_recursive_opt = ''
    let g:unite_source_rec_async_command = ['ag', '--nocolor', '--nogroup', '-g', '']
endif
let g:unite_enable_ignore_case = 1
let g:unite_prompt = '» '
let g:unite_source_history_yank_enable = 1
let g:unite_split_rule = 'bot'
let g:unite_winheight = 15
let mapleader = ' '
nnoremap <silent><leader>b :Unite buffer<cr>
nnoremap <silent><leader>b :Unite buffer<cr>
nnoremap <silent><leader>f :Unite -start-insert file_rec/async file_rec/git buffer<cr>
vnoremap <silent><leader>f :Unite -start-insert file_rec/async file_rec/git buffer<cr><c-r>*
nnoremap <silent><leader>g :Unite -start-insert file_rec/git buffer<cr>
vnoremap <silent><leader>g :Unite -start-insert file_rec/git buffer<cr><c-r>*
nnoremap <silent><leader>u :Unite grep:.:<cr>
" search selected text
vnoremap <silent><leader>u :Unite grep:.::<c-r>*<cr>
" search current filename
nnoremap <silent><leader>c :Unite grep:.::<c-r>=expand('%:t')<cr><cr>
" search Angular.js keywords
nnoremap <silent><leader>a :Unite -start-insert grep:.::\\.(filter\|provider\|controller\|directive\|factory\|service)\\(\\'.+\\'<cr>



" syntax and file type detection
syntax on
filetype on

au BufNewFile,BufRead *.xaml set filetype=xml
au BufNewFile,BufRead *.md set filetype=markdown

autocmd FileType python set omnifunc=pythoncomplete#Complete
autocmd FileType php set omnifunc=phpcomplete#CompletePHP
autocmd FileType javascript set omnifunc=javascriptcomplete#CompleteJS
autocmd FileType html set omnifunc=htmlcomplete#CompleteTags
autocmd FileType css set omnifunc=csscomplete#CompleteCSS
autocmd FileType xml set omnifunc=xmlcomplete#CompleteTags
autocmd BufReadPost * if line("'\"") > 1 && line("'\"") <= line("$") | exe "normal! g`\"" | endif



set hidden " do not close file buffers when opening a new one, use :ls to list buffers
set hlsearch " highlight search
set autoread " automatically update file if changed from the outside
set backspace=indent,eol,start " make backspace clear other than new text also
set guicursor=n-v-c:hor25-Cursor " set cursor size
set nocp " nocompatible: enables features which are not Vi compatible
set ic " set ignore case on searches, remedy with set noic or use \C in search term
set clipboard=unnamedplus " alias unnamed register to +
set number  " line numbering
set textwidth=0 " define text width if autowrapping: 0 disables autowrap, wraps to 80 on manual wrap (gq)
let g:leave_my_textwidth_alone=1 " don't autowrap lines while typing
set go-=T " hide toolbar
set go-=m " hide menu
set go-=t " disable tear-off menus
set go-=r " disable right scroll bar



" uppercase commands for typos
command W w
command Q q
command WQ wq
command Wq wq

" convert Alt Gr + space to normal space
inoremap Â  <space>



" select all
inoremap <c-a> <esc>gg<s-v>G

" wrap to next/previous line on arrow key presses
inoremap <silent> <left> <c-r>=col('.') == 1 ? "\<lt>c-o>gkg$" : "\<lt>left>"<cr>
inoremap <silent> <right> <c-r>=col('.') == col('$') ? "\<lt>c-o>gjg0" : "\<lt>right>"<cr>

" emulate common shift + keys functionality
inoremap <silent> <s-up> <c-o>v<up>
vnoremap <silent> <s-up> <up>
inoremap <silent> <s-down> <c-o>v<down>
vnoremap <silent> <s-down> <down>
inoremap <silent> <s-left> <c-r>=col('.') == 1 ? "\<lt>c-o>gkg$v" : "\<lt>c-o>hv"<cr>
inoremap <silent> <s-right> <c-r>=col('.') == col('$') ? "\<lt>c-o>gjg0v" : "\<lt>c-o>v"<cr>
inoremap <silent> <s-home> <left><c-o>v<home>
inoremap <silent> <s-end> <c-o>v<end><left>

" common copy hotkeys
nnoremap <c-insert> V"+y<esc>:echo "Line copied."<cr>
vnoremap <c-insert> "+y<esc>:echo "Copied."<cr>
inoremap <c-insert> <esc>`^bve
vnoremap <c-c> "+y<esc>:echo "Copied."<cr>

" common insert hotkeys
nnoremap <s-insert> "+P
inoremap <s-insert> <esc>"+pa
inoremap <c-v> <c-r>=col('.') == col('$') ? "\<lt>esc>`^\"+pa" : "\<lt>esc>`^\"+Pa"<cr>
" replace contents
vnoremap <s-insert> "_x"+P

" common search hotkey
inoremap <c-f> <esc>/
nnoremap <c-f> /

" common search and replace hotkey
nnoremap <c-h> :%s//g\|:silent noh<left><left><left><left><left><left><left><left><left><left><left><left><left><left>
vnoremap <c-h> :s//g\|:silent noh<left><left><left><left><left><left><left><left><left><left><left><left><left><left>



" up/down
inoremap <c-k> <up>
inoremap <c-j> <down>

" move screen lines
inoremap <home> <c-o>g0
inoremap <end> <c-o>g$
" these disable functionality with menus, use mapped up/down
inoremap <up> <c-o>gk
inoremap <down> <c-o>gj



" uppercase word under cursor
nnoremap <silent> <f8> gUiw

" toggle menubar
nnoremap <f1> :if &go=~#'m'<bar>set go-=m<bar>else<bar>set go+=m<bar>endif<cr>
inoremap <f1> <esc>:if &go=~#'m'<bar>set go-=m<bar>else<bar>set go+=m<bar>endif<cr>

" insert mode toggles, grave moves to ^ (editing mark)
inoremap <c-space> <esc>`^
nnoremap <c-space> i
nnoremap <enter> i<c-r>=col('.') == col('$')-1 ? "\<lt>right>" : ""<cr>

" center search matches
nnoremap n nzz
nnoremap N Nzz

" clear search highlight
nnoremap <silent> <c-n> :silent noh<cr>

" prefix (eg. for commenting) visual lines
vnoremap <c-m> :s/^//\|:silent noh<left><left><left><left><left><left><left><left><left><left><left><left><left>



" relative line numbering came in vi 7.3
if version >= 703
    function! ToggleRelativeNumbering()
        if &rnu
            set rnu!
            echo "Absolute numbering set."
        else
            set relativenumber
            echo "Relative numbering set."
        endif
        return ''
    endfunction
    inoremap <silent> <f3> <c-r>=ToggleRelativeNumbering()<cr>
    nnoremap <silent> <f3> :call ToggleRelativeNumbering()<cr>
endif

function! ToggleWrap()
    if &wrap
        set nowrap
        echo "No wrap set."
    else
        set wrap
        echo "Wrap set."
    endif
    return ''
endfunction
inoremap <silent> <f2> <c-r>=ToggleWrap()<cr>
nnoremap <silent> <f2> :call ToggleWrap()<cr>

" print file info
function! PrintFileInfo()
    redir => filename
    silent !echo %:p
    redir END
    let filename = substitute(filename, '.*\n\(.*\n\)', '\1', '')
    let n = confirm(filename . "\n" . &fileencoding . "\n" . &ff, "", 1, "Info")
endfunction
nnoremap <f5> :call PrintFileInfo()<cr>

" set file encoding dialog
function! SetFileEncoding()
    let n = confirm("Select file encoding:\n(current: " . &fileencoding . ")", "&UTF-8\n&ISO-8859-1\n&Cancel", 3, "Question")
    if n == 1
        set fileencoding=utf-8
    elseif n == 2
        set fileencoding=iso-8859-1
    endif
endfunction
nnoremap <f6> :call SetFileEncoding()<cr>

" set file format dialog
function! SetFileFormat()
    let n = confirm("Select format for writing the file:\n(current: " . &ff . ")", "&Unix\n&Dos\n&Mac\n&Cancel", 4, "Question")
    if n == 1
        set ff=unix
    elseif n == 2
        set ff=dos
    elseif n == 3
        set ff=mac
    endif
endfunction
nnoremap <f7> :call SetFileFormat()<cr>

" font zooming
if zoom_functions == "1"

    let g:zoom_level=0

    function! GrowFont()
        let fontsize = substitute(&guifont, '[^0-9]', '', 'g')
        let fontname = substitute(&guifont, '[0-9 ]', '', 'g')
        let fontname = substitute(fontname, ':h:cANSI', '', 'g')
        if g:zoom_level == 15
            return ''
        endif
        let fontsize = fontsize + 2
        if g:zoom_level == 0
            let fontname = g:daltfont
            let fontsize = 10
        endif
        let g:zoom_level = g:zoom_level + 1
        if has("win32")
            let &guifont = fontname . ":h" . fontsize . ":cANSI"
        else
            let &guifont = fontname . " " . fontsize
        endif
        let &lines=g:dlines
        let &columns=g:dcols
        echo "Font set to: " &guifont
        return ''
    endfunction

    function! ShrinkFont()
        let fontsize = substitute(&guifont, '[^0-9]', '', 'g')
        let fontname = substitute(&guifont, '[0-9 ]', '', 'g')
        let fontname = substitute(fontname, ':h:cANSI', '', 'g')
        if g:zoom_level == 0
            return ''
        endif
        let fontsize = fontsize - 2
        if g:zoom_level == 1
            let fontname = g:dfont
            let fontsize = 12
        endif
        let g:zoom_level = g:zoom_level - 1
        if has("win32")
            let &guifont = fontname . ":h" . fontsize . ":cANSI"
        else
            let &guifont = fontname . " " . fontsize
        endif
        let &lines=g:dlines
        let &columns=g:dcols
        echo "Font set to: " &guifont
        return ''
    endfunction

    inoremap <silent> <c-kplus> <c-r>=GrowFont()<cr>
    nnoremap <silent> <c-kplus> :call GrowFont()<cr>
    inoremap <silent> <c-kminus> <c-r>=ShrinkFont()<cr>
    nnoremap <silent> <c-kminus> :call ShrinkFont()<cr>
    inoremap <silent> <c-mousedown> <c-r>=GrowFont()<cr>
    nnoremap <silent> <c-mousedown> :call GrowFont()<cr>
    inoremap <silent> <c-mouseup> <c-r>=ShrinkFont()<cr>
    nnoremap <silent> <c-mouseup> :call ShrinkFont()<cr>
endif

" Unite sources
call unite#custom_source('menu', 'matchers', ['matcher_fuzzy'])
call unite#custom_source('source', 'matchers', ['matcher_fuzzy'])
call unite#custom_source('outline', 'matchers', ['matcher_fuzzy'])
call unite#custom_source('history/yank', 'matchers', ['matcher_fuzzy'])
call unite#custom_source('file_rec,file_rec/async,file_mru,file,buffer,grep',
     \ 'ignore_pattern', join([
     \ '\.git/',
     \ '\.tscache/',
     \ 'git5/.*/review/',
     \ 'google/obj/',
     \ 'tmp/',
     \ 'lib/Cake/',
     \ 'node_modules/',
     \ 'vendor/',
     \ 'Vendor/',
     \ 'app_old/',
     \ 'acf-laravel/',
     \ 'plugins/',
     \ 'bower_components/',
     \ '.sass-cache',
     \ 'web/wp',
     \ 'nimcache',
     \ '.toc$',
     \ '.bak$',
     \ '.log$',
     \ '.out$',
     \ '.aux$',
     \ '.a$',
     \ '.o$',
     \ '.obj$',
     \ '.so$',
     \ '.lib$',
     \ '.jar$',
     \ '.pdf$'
     \ ], '\|'))

